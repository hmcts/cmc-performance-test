plugins {
    id 'org.owasp.dependencycheck' version '6.0.1'
    id 'scala'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'com.github.lkishalmi.gatling' version '3.0.2'
    id 'groovy'
    id 'com.gradle.plugin-publish' version '0.12.0'
    id 'maven-publish'
}

//apply plugin: 'java'
apply plugin: 'scala'
group 'uk.gov.hmcts.reform'
version = '1.0'
sourceCompatibility = 11

jar {
    manifest {
        attributes 'Implementation-Title': 'Project Name Performance Tests',
                'Implementation-Version': version
    }
}

dependencyCheck {
    // Specifies if the build should be failed if a CVSS score above a specified level is identified.
    // range of 0-10 fails the build, anything greater and it doesn't fail the build
    failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 0 : 11
    suppressionFile = 'dependency-check-suppressions.xml'
}

repositories {
    mavenCentral()
}

dependencyManagement {
    dependencies {
        dependency group: 'io.pebbletemplates', name: 'pebble', version: '3.1.4'

        dependencySet(group: 'org.apache.logging.log4j', version: '2.13.3') {
            entry 'log4j-api'
            entry 'log4j-core'
            entry 'log4j-to-slf4j'
        }

        dependencySet(group: 'org.scala-lang', version: '2.12.12') {
            entry 'scala-compiler'
            entry 'scala-library'
            entry 'scala-reflect'
        }

        // solves CVE-2019-12086
        // remove once spring manager incorporates this changes
        dependencySet(group: 'com.fasterxml.jackson.core', version: '2.11.2') {
            entry 'jackson-core'
            entry 'jackson-databind'
        }

        // CVE-2019-16869 - brought in by qpid-jms-client
        dependencySet(group: 'io.netty', version: '4.1.61.Final') {
            entry 'netty-buffer'
            entry 'netty-codec'
            entry 'netty-codec-dns'
            entry 'netty-codec-http'
            entry 'netty-codec-http2'
            entry 'netty-codec-socks'
            entry 'netty-common'
            entry 'netty-handler'
            entry 'netty-handler-proxy'
            entry 'netty-resolver'
            entry 'netty-resolver-dns'
            entry 'netty-transport-native-epoll'
            entry 'netty-transport-native-kqueue'
            entry 'netty-transport-native-unix-common'
            entry 'netty-transport'
        }
    }
}

dependencies {
    compile 'org.scala-lang:scala-library:2.12.12'
    gatlingCompile 'com.warrenstrange:googleauth:1.5.0'
    compile group: 'io.gatling', name: 'gatling-app', version: '3.3.1'
    compile group: 'io.gatling', name: 'gatling-recorder', version: '3.3.1'
    compile group: 'io.gatling.highcharts', name: 'gatling-charts-highcharts', version: '3.3.1'
}

gatling {
    toolVersion '3.1.3'
    scalaVersion '2.12.12'
    simulations = [ 'uk.gov.hmcts.reform.cmc.performance.simulations.CMCSimulation']
}

test {
    testLogging {
        showStandardStreams = true
    }
    dependsOn("publishToMavenLocal")
    systemProperty "com.github.lkishalmi.gatling.version", project.version
}

pluginBundle {
    website = 'https://github.com/lkishalmi/gradle-gatling-plugin'
    vcsUrl = 'https://github.com/lkishalmi/gradle-gatling-plugin'
    description = 'Gatling Simulation Execution'
    tags = ['gatling', 'load test', 'stress test', 'performance test', 'scala']
}
